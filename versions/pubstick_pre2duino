import cc.arduino.*;
import org.firmata.*;
import processing.serial.*;
import java.util.Map;
import java.util.Arrays;
import processing.video.*;
import ddf.minim.*;

// ** Audio config
Minim minim;
AudioPlayer letsPlay;
Minim minim2;
AudioPlayer shotMade;
Minim minim3;
AudioPlayer shotMissed;
Minim minim4;
AudioPlayer playerWinCelebration;

// ** Arduino configx
Arduino arduino;
int lastShotMade;                             // used for target switches to reset, log shot made time
final int DELAY_TIME = 1000;                  // Time for switch reset after shot is made
int vidIndex;                // Temp Variable
// ** Game config vars
final int holePins[] = { 3, 4, 5, 6, 7};     // the pins that the sensors are attached to 
final int holeValues[] = { 1, 2, 3, 5, 7 };
final int ballCount = 10;                     // total balls per turn
final int bustPenalty = 14;
final int turnDuration = 177000;              // duration of each turn
final int maxNumberOfPlayers = 4;
int numberOfPlayers = 1;                      // number of players per game

// ** Player state
Player players[];                             // Holds the player objects
int currentPlayerIndex;                       // index of current player in the Player array

// ** Game state
// Only of of these will be true at a time. ONLY update using the updateGameStateBooleanToTrue() method
// Each has a corresponding screen, displayed with a method using the naming convention
// show[gamestate]Screen (eg. showGameOnStandbyScreen())
boolean gameOnStandby = false;
boolean showingGameRules = false;
boolean selectingPlayers = false;
boolean gameRunning = false;
boolean gameFinished = false;
boolean startPlayerTimerAfterAnimations = false;

// Amount of time to wait before drawing anything
// Prevents issues with attempting to run animations
// before the game has begun
final int GAME_STARTUP_TIME = 5000;

// ** Visual and animation vars
PFont font;

int gameIntroAnimationStartTime;
final int GAME_INTRO = 2500;

int advancePlayerAnimationStartTime;
final int ADVANCE_PLAYER = 4000;

int shotMadeAnimationStartTime;
final int SHOT_MADE = 2000;


int shotMissedAnimationStartTime;
final int SHOT_MISSED = 2500;

int gameOverAnimationStartTime;
final int GAME_OVER = 2500;

final int SCREEN_WIDTH = 1920;
final int SCREEN_HEIGHT = 1080;
ArrayList<Movie> queuedAnimations = new ArrayList<Movie>();
Movie currAnim;
float startAnimTime;

void setup() {
  // Visual setup
  //fullScreen();
  size(1920, 1080);
  frameRate(30);
  font = createFont("Arial", 16, true);
  textFont(font);
  initSvgDrawing();
  initImages();
  initBackgroundScreenImages();
  initAnimations();

  // Arduino initializer
  arduino = new Arduino(this, Arduino.list()[1], 57600);
  for (int i = 0; i < holePins.length; i++) {
    int pinHit = holePins[i];
    arduino.pinMode(pinHit, Arduino.INPUT);
  }
  arduino.pinMode(8, Arduino.INPUT);  //MissedPin
  arduino.pinMode(11, Arduino.INPUT); //Button
  arduino.pinMode(12, Arduino.INPUT); //Button
  arduino.pinMode(13, Arduino.INPUT); //Button

  minim = new Minim(this);
  minim2 = new Minim(this);
  minim3 = new Minim(this);
  minim4 = new Minim(this);

  // Game vars setup
  updateGameState("gameOnStandby");
}

void draw() {
 
  
  if (arduino.digitalRead(11) == Arduino.HIGH && arduino.digitalRead(12) == Arduino.HIGH && arduino.digitalRead(13) == Arduino.HIGH) {
     println("3");
     delay(500);
     updateGameState("gameOnStandby");
  }

  //OK BUTTON
  if (arduino.digitalRead(11) == Arduino.HIGH) {
    if (gameOnStandby) {
      updateGameState("showingGameRules");
      letsPlay = minim.loadFile("casino.mp3");
      letsPlay.play();
    } else if (showingGameRules) {
      updateGameState("selectingPlayers");
    } else if (selectingPlayers) {
      startNewGame(numberOfPlayers, ballCount);
    } else if (gameFinished) {
      updateGameState("gameOnStandby");
    }
    delay(250);
  }

  //LEFT RIGHT SELECTION
  if (!gameRunning) {
    if (arduino.digitalRead(13) == Arduino.HIGH && numberOfPlayers < maxNumberOfPlayers) {
      numberOfPlayers = numberOfPlayers + 1;
      delay(100);
    } else if (arduino.digitalRead(12) == Arduino.HIGH && numberOfPlayers > 1) {
      numberOfPlayers = numberOfPlayers - 1;
      delay(100);
    }
  }

  if (currAnim != null) {
    if (millis() > startAnimTime + currAnim.duration()*1000) {
      currAnim = null;
      queuedAnimations.remove(0);
      if (queuedAnimations.isEmpty() && startPlayerTimerAfterAnimations) {
        currentPlayer().startPlayerTimer();
        startPlayerTimerAfterAnimations = false;
      }
    }
  }

  if (queuedAnimations.size() > 0) {
    if (currAnim == null) {
      currAnim = queuedAnimations.get(0);
      currAnim.play();
      startAnimTime = millis();
    } else if (currAnim != null) {
      image(currAnim, 0, 0);
    }
  } else if (gameOnStandby) {
    showGameOnStandbyScreen();

    // Showing rules screen
  } else if (showingGameRules) {
    showGameRulesScreen();

    // Selecting players actions
  } else if (selectingPlayers) {
    showSelectingPlayersScreen();

    // Game running actions
  } else if (gameRunning) {
    showGameRunningScreen();

    // checking all sensors in the holes for a successful putt
    for (int i = 0; i <= 4; i++) {
      int pin = i + 3;
      if (arduino.digitalRead(pin) == Arduino.LOW && currentPlayer().ballsLeft > 0 && (millis() - lastShotMade >= DELAY_TIME)) {
        shotMade(pin);
      }
    }

    if (arduino.digitalRead(8) == Arduino.LOW) {
      shotMissed();
    }

    if (currentPlayer().isTimerOver()) {
      advancePlayer();
    }

    // Game over actions
  } else if (gameFinished) {
    showGameFinishedScreen();
  }

}

void shotMade(int pinNumber) {
  // non-game-state variables
  lastShotMade = millis();
  shotMadeAnimationStartTime = millis();
  shotMade = minim.loadFile("chaChing.wav");
  shotMade.play();

  //trigger different animations based on pinNumber
  int vidToPlay = pinNumber - 3;
  queueAnimation(allScoreVideos[vidToPlay]);

  // Updating game vars
  currentPlayer().madeShot(pinNumber);
  checkTurnStatus();
}

void checkTurnStatus() {
  // TODO check if player bust or blackjack, queueAnimation(animation);
  if (currentPlayer().score == 21) {
    queueAnimation(blackjackAnimation);
  }
  if (currentPlayer().ballsLeft == 0 || currentPlayer().score == 21) {
    advancePlayer();
  }
}

void queueAnimation(Movie m) {
  m.stop();
  queuedAnimations.add(m);
}

void shotMissed() {
  queueAnimation(missAnimation);
  shotMissed = minim.loadFile("whiff.mp3");
  shotMissed.play();
  currentPlayer().missedShot();
  checkTurnStatus();
}

void startPlayer() {
  startPlayerTimerAfterAnimations = true;
  //todo queue animation
  int playerIndex = currentPlayer().id - 1;

  queueAnimation(allPlayerVideos[playerIndex]);
  updateGameState("gameRunning");

  lastShotMade = millis();
  setScoreImageToZero();
  lastUpdatedTimeAt = millis();
}

void advancePlayer() {
  currentPlayer().endTurn();

  // Two scenarios when advancing player
  // 1. More players left, so advance to next player
  // 2. This is the last player, game over


  if (currentPlayerIndex < (players.length-1)) {
    currentPlayerIndex++;
    startPlayer();
  } else if (currentPlayerIndex == (players.length-1)) {
    gameOverAnimationStartTime = millis();
    updateGameState("gameFinished");
  }
}

Player currentPlayer() {
  return players[currentPlayerIndex];
}

// Simulating mouse click is start game
void mousePressed() {
  if (gameOnStandby) {
    updateGameState("showingGameRules");
  } else if (showingGameRules) {
    updateGameState("selectingPlayers");
  } else if (selectingPlayers) {
    startNewGame(numberOfPlayers, ballCount);
  } else if (gameFinished) {
    updateGameState("gameOnStandby");
  }
}

// If selecting players: Simulating 1-5 as selecting number of players
// If game is running: Simulating 1-5 on the keyboard are holes 1-5
void keyPressed() {
  int keyVal = Character.getNumericValue(key);
  if (keyVal >= 1 && keyVal <= 5 && !isAnimationRunning()) {
    print("\t" + gameRunning);
    if (gameRunning) {
      int pinHit = holePins[keyVal-1]; // Dummy conversion to the pin value from the key pressed.
      vidIndex = keyVal - 1;
      print("\t" + pinHit + "- -- " + vidIndex);
      shotMade(pinHit);
    } else if (selectingPlayers && keyVal < 5) {
      numberOfPlayers = keyVal;
    }
  }
}

// Called every time a new frame is available to read
void movieEvent(Movie m) {
  m.read();
}


void stop(){
  minim.stop();
  minim2.stop();
  minim3.stop();
  minim4.stop();
  letsPlay.close();
  shotMade.close();
  shotMissed.close();
  playerWinCelebration.close();
  super.stop();
}
